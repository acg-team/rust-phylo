newick        =  { SOI ~ ( tree ~ ";" ) * ~ EOI | SOI ~ ( "(" ~ tree ~ ")" ~ ";" ) * ~ EOI }
tree          =  { rooted | unrooted }
rooted        =  { node }
unrooted      =  { "(" ~ node ~ "," ~ node ~ "," ~ node ~ ")" }
node          = _{ leaf | internal }
leaf          =  { label ~ branch_length? }
internal      =  { "(" ~ node ~ "," ~ node ~ ")" ~ label? ~ branch_length? }
label         =  { safe_chars+ }
safe_chars    = _{ !(":" | "," | ";" | "(" | ")" | "[" | "]" | WHITESPACE) ~ ANY }
branch_length =  { ":" ~ float }
float         =  { sign? ~ digit* ~ ("." ~ digit*)? ~ (^"e" ~ sign? ~ digit+)?}
sign          = _{ "+" | "-" }
digit         = _{ ASCII_DIGIT }
nonzero_digit = _{ ASCII_NONZERO_DIGIT }
comment       = _{ "[" ~ (!"]" ~ ANY)* ~ "]" }
WHITESPACE    = _{ " " | "\t" | NEWLINE }